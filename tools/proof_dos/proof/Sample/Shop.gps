	SIMULATE
*
* ---------------------------------------------------------------------
*
* Proof Macros
*
* ---------------------------------------------------------------------
*
	REAL            &ATFTIME
*
PUTTIME STARTMACRO
	TEST NE         AC1,&ATFTIME,*+3
	BPUTPIC         FILE=ATF,(AC1)
TI *.*
	BLET            &ATFTIME=AC1
	ENDMACRO
*
CREATE  STARTMACRO
PUTTIME MACRO
	BPUTPIC         FILE=ATF,(#B)
CR #A *
	ENDMACRO
*
DESTROY STARTMACRO
PUTTIME MACRO
	BPUTPIC         FILE=ATF,(#A)
DES *
	ENDMACRO
*
PLACEIN STARTMACRO
PUTTIME MACRO
	BPUTPIC         FILE=ATF,(#A,#C)
PL * IN #B*
	ENDMACRO
*
PLACEON STARTMACRO
PUTTIME MACRO
	BPUTPIC         FILE=ATF,(#A)
PL * ON #B
	ENDMACRO
*
SETCLAS STARTMACRO
PUTTIME MACRO
	BPUTPIC         FILE=ATF,(#A)
SET * CLASS #B
	ENDMACRO
*
ALLBARB STORAGE 6
*
*       ONE-LINE, SINGLE-SERVER QUEUEING MODEL
*
	GENERATE        RVEXPO(1,2.75)  ARRIVALS EVERY 2.75 (AVG.) MINUTES
	ADVANCE
	SEIZE   DOORWAY
	ADVANCE 0.5     HANG UP COAT
	RELEASE DOORWAY
	QUEUE   JOEQ
CREATE  MACRO   waiting,XID1
PLACEON MACRO   XID1,line
	ENTER   ALLBARB
	ADVANCE 0.2     TRAVERSE THE WAITING LINE
	DEPART  JOEQ
	SELECT MIN      BARB,1,6,,F
	SEIZE   PH(BARB)        CAPTURE THE BARBER
SETCLAS MACRO   XID1,customer
PLACEIN MACRO   XID1,chair,PH(BARB)
	ADVANCE 15,3    HAIRCUT TAKES 15 +- 3 MINUTES
	RELEASE PH(BARB)        FREE THE BARBER
DESTROY MACRO   XID1
	LEAVE   ALLBARB
	TERMINATE      1       EXIT THE SHOP
*
ATF     FILEDEF 'SHOP.ATF'
*
*       GENERATE       5
	BLET           PH(COUNT)=10
*
OBSERVE BLET    PH(UTIL1)=PH(UTIL1)+F(1)
	BLET    PH(UTIL2)=PH(UTIL2)+F(2)
	BLET    PH(UTIL3)=PH(UTIL3)+F(3)
	BLET    PH(UTIL4)=PH(UTIL4)+F(4)
	BLET    PH(UTIL5)=PH(UTIL5)+F(5)
	BLET    PH(UTIL6)=PH(UTIL6)+F(6)
	BLET    PH(UTIL)=PH(UTIL)+S(ALLBARB)
	ADVANCE 5
	LOOP    (COUNT)PH,OBSERVE
*
	BPUTPIC FILE=ATF,LINES=9,(AC1,_
		10*PH(UTIL1),10*PH(UTIL2),10*PH(UTIL3),_
		10*PH(UTIL4),10*PH(UTIL5),10*PH(UTIL6),_
		10*(PH(UTIL)/6.),10*Q(JOEQ))
TIME *
SET BAR UTIL1 TOP *
SET BAR UTIL2 TOP *
SET BAR UTIL3 TOP *
SET BAR UTIL4 TOP *
SET BAR UTIL5 TOP *
SET BAR UTIL6 TOP *
SET BAR UTIL RIGHT *
SET BAR QUEUE RIGHT *
*
	TERMINATE       0
*
	START   1200
	END
