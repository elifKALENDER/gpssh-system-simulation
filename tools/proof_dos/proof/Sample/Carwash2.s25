''      CARWASH MODEL - SIMSCRIPT II.5 IMPLEMENTATION

PREAMBLE
        PROCESSES
                EVERY CAR HAS AN ID

        RESOURCES INCLUDE CHAIN
        
        DEFINE ID AS AN INTEGER VARIABLE

        DEFINE NEXTCAR AS AN INTEGER VARIABLE
END

MAIN
        CREATE EVERY CHAIN(1)
        LET U.CHAIN(1) = 1      '' SINGLE SERVER

        ACTIVATE A CAR CALLED NEXTCAR AT EXPONENTIAL.F(2.2, 1) '' 
FIRST ARRIVAL
        LET ID(NEXTCAR) = 1

        USE 10 FOR OUTPUT

        START SIMULATION
END

PROCESS CAR 

        ACTIVATE A CAR CALLED NEXTCAR IN EXPONENTIAL.F(2.2, 1) 
UNITS     '' N+1ST ARRIVAL
        LET ID(NEXTCAR) = ID(CAR) + 1

        NOW PCREATE("CAR",ID(CAR))
        NOW PLACEON(ID(CAR), "ENTRY")

        WAIT 0.25 UNITS         '' MOVE TO ENTRY

        REQUEST 1 CHAIN(1)      '' SINGLE SERVER

        NOW PLACEON(ID(CAR), "CHAIN")

        WORK 2.0 UNITS          '' WASH TIME

        RELINQUISH 1 CHAIN(1)   '' ALLOW ANOTHER CAR IN

        WORK 1.5 UNITS          '' DRYING TIME

        NOW PLACEON(ID(CAR), "EXIT")

        WORK 0.25 UNITS          '' EXIT TIME

        NOW PDESTROY(ID(CAR))

        IF ID(CAR) >= 100
                PRINT 2 LINES WITH TIME.V THUS
TIME ***.****
END
                STOP
        ALWAYS
END

ROUTINE PCREATE(CLASS,ID)

        DEFINE CLASS AS A TEXT VARIABLE
        DEFINE ID AS AN INTEGER VARIABLE

        NOW PUTTIME

        PRINT 1 LINE WITH CLASS,  ID THUS
Create ******** ********

        RETURN
END

ROUTINE TO PDESTROY GIVEN ID

        DEFINE ID AS AN INTEGER VARIABLE

        NOW PUTTIME

        PRINT 1 LINE WITH ID THUS
Destroy ********

        RETURN
END

ROUTINE TO PLACEON GIVEN ID, PATH

        DEFINE ID AS AN INTEGER VARIABLE
        DEFINE PATH AS A TEXT VARIABLE

        NOW PUTTIME

        PRINT 1 LINE WITH ID, PATH THUS
Place ******** on ********

        RETURN
END

ROUTINE TO PLACEAT GIVEN ID, X, Y

        DEFINE ID AS AN INTEGER VARIABLE
        DEFINE X AND Y AS REAL VARIABLES

        NOW PUTTIME

        PRINT 1 LINE WITH ID, X, AND Y THUS
Place ******** at ****.** ****.**

        RETURN
END

ROUTINE TO PUTTIME

        DEFINE TIME.OF.LAST.OUTPUT AS A SAVED REAL VARIABLE

        IF TIME.V > TIME.OF.LAST.OUTPUT
                PRINT 1 LINE WITH TIME.V THUS
Time ****.***
                TIME.OF.LAST.OUTPUT = TIME.V
        ALWAYS
        RETURN
END

ROUTINE TO PWRITE GIVEN MSG.NAME, MSG.TEXT

        DEFINE MSG.NAME AND MSG.TEXT AS TEXT VARIABLES

        NOW PUTTIME

        PRINT 1 LINE WITH MSG.NAME AND MSG.TEXT THUS
Write ******** *************************************

        RETURN
END

ROUTINE TO SETCOLOR GIVEN ID, COLOR.NAME

        DEFINE ID AS AN INTEGER VARIABLE
        DEFINE COLOR.NAME AS A TEXT VARIABLE

        NOW PUTTIME

        PRINT 1 LINE WITH ID AND COLOR.NAME THUS
Set ******** color ********

        RETURN
END


