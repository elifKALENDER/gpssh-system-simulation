       SIMULATE
*
*      Define Matrix
*
 SERV  MATRIX    MH,2,2
       INITIAL   MH$SERV(1,1),6/MH$SERV(1,2),7 Mean service times for inspector 1
       INITIAL   MH$SERV(2,1),8/MH$SERV(2,2),9 Mean service times for inspector 2
*
*      Storage Declaration Segment
*
       STORAGE    S(WKR2),2/S(WKR5),3/S(INSPECT),2
*
*      GPSS/H Block Section
*
*         Short Sleeves Shirt
*
       GENERATE   8,1,,,,1PH  Short sleeves t-shirt arrives
       ASSIGN     SLEEVES,1,PH Set parameter 1 to code 1
       TRANSFER   ,STAGE      Send to ADVANCE block
*
*         Long Sleeves Shirt
*
       GENERATE   8,1,,,,1PH  Long sleeves t-shirt arrives
       ASSIGN     SLEEVES,2,PH Set parameter 1 to code 2
*
*         Staging Area
*
 STAGE ADVANCE    2           Transportation time to the staging area
       SEIZE      SERVER      Capture the line server
       ADVANCE    3.75,1      Prepare a bundle
       RELEASE    SERVER      Release the line server
*
*         Sewing Line
*
       SEIZE      WKR1        Capture work station 1
       ADVANCE    3.5,1       Close first shoulder
       RELEASE    WKR1        Free work station 1
*
       ENTER      WKR2        Capture 1 of work station 2
       ADVANCE    7,2         Sew collar
       LEAVE      WKR2        Free 1 of work station 2
*
       SEIZE      WKR3        Capture work station 3
       ADVANCE    3,0.75      Cover seam
       RELEASE    WKR3        Free work station 3
*
       SEIZE      WKR4        Capture work station 4
       ADVANCE    3.25,1      Close second shoulder
       RELEASE    WKR4        Free work station 4
* 
       ENTER      WKR5        Capture 1 of work station 5 
       ADVANCE    11,3        Set sleeves
       LEAVE      WKR5        Free 1 of work station 5
*
*         Inspection Stations
*
       ENTER      INSPECT     Capture 1 inspection station
       TEST E     0,F(INSP1),OTHER Is inspector 1 idle?
       SEIZE      INSP1       Capture inspector 1
       ADVANCE    MH(SERV,1,PH(SLEEVES)),2 Inspect t-shirts
       RELEASE    INSP1       Release inspector 1
       TRANSFER   ,BACK       Go to Block BACK
 OTHER SEIZE      INSP2       Capture inspector 2
       ADVANCE    MH(SERV,2,PH(SLEEVES)),1 Inspect t-shirts
       RELEASE    INSP2       Release inspector 2
 BACK  LEAVE      INSPECT     Free 1 inspection station
       TRANSFER   .05,,REJ    5% are rejected
       TEST E     PH(SLEEVES),1,TYPE2 Is t-shirt short sleeve?
 TYPE1 TERMINATE  1           Short sleeve t-shirts exit the system
 TYPE2 TERMINATE  1           Long sleeve t-shirts exit the system
 REJ   TERMINATE              Bad t-shirts exit the system 
*
*      GPSS/H Control Statements
*
       START      100         Set TG1 to 100 
       END
