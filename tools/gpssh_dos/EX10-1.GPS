       SIMULATE
*
*      Define Ampervariables
*
       INTEGER    &M(2),&S(2)      &arrays for inspection times
       REAL       &MSET(2),&SSET(2)  &arrays for Set Sleeves time
       INTEGER    &I                 &Index for GETLIST loop
       INTEGER    &NSETSL          Number of workers at Set Sleeve
*
 DATA  FILEDEF    'SERVICE.DAT'
       GETLIST    FILE=DATA,(&M(1))  Read mean inspection time for inspector 1
       GETLIST    FILE=DATA,(&M(2))  Read mean inspection time for inspector 2
       GETLIST    FILE=DATA,(&S(1))  Read inspection time spread for inspector 1
       GETLIST    FILE=DATA,(&S(2))  Read inspection time spread for inspector 2
*
       GETLIST    FILE=DATA,((&MSET(&I),&SSET(&I),&I=1,2))  Read Set Sleeve time
       GETLIST    FILE=DATA,(&NSETSL) Read number of workers at Set Sleeve
*
*      Storage Declaration Segment
*
       STORAGE    S(WKR2),2/S(WKR5),&NSETSL/S(INSPECT),2
*
*      GPSS/H Block Section
*
*         Short Sleeves Shirt
*
       GENERATE   8,1,,,,1PH  Short sleeves t-shirt arrives
       ASSIGN     SLEEVES,1,PH      Set parameter SLEEVES to code 1   
       TRANSFER   ,STAGE      Send to ADVANCE block
*
*         Long Sleeves Shirt
*
       GENERATE   8,1,,,,1PH  Long sleeves t-shirt arrives
       ASSIGN     SLEEVES,2,PH      Set parameter SLEEVES to code 2
*
*         Staging Area
*
 STAGE ADVANCE    2           Transportation time to the staging area
       SEIZE      SERVER      Capture the line server
       ADVANCE    3.75,1      Prepare a bundle
       RELEASE    SERVER      Release the line server
*
*         Sewing Line
*
       SEIZE      WKR1        Capture work station 1
       ADVANCE    3.5,1       Close first shoulder
       RELEASE    WKR1        Free work station 1
*
       ENTER      WKR2        Capture 1 of work station 2
       ADVANCE    7,2         Sew collar
       LEAVE      WKR2        Free 1 of work station 2
*
       SEIZE      WKR3        Capture work station 3
       ADVANCE    3,0.75      Cover seam
       RELEASE    WKR3        Free work station 3
*
       SEIZE      WKR4        Capture work station 4
       ADVANCE    3.25,1      Close second shoulder
       RELEASE    WKR4        Free work station 4
* 
       ENTER      WKR5        Capture 1 of work station 5 
       ADVANCE    &MSET(PH(SLEEVES)),&SSET(PH(SLEEVES))    Set sleeves
       LEAVE      WKR5        Free 1 of work station 5
*
*         Inspection Stations
*
       ENTER      INSPECT     Capture 1 inspection station
       TEST E     0,F(INSP1),OTHER Is inspector 1 idle?
       SEIZE      INSP1       Capture inspector 1
       ADVANCE    &M(1),&S(1) Inspect shirts
       RELEASE    INSP1       Release inspector 1
       TRANSFER   ,BACK       Go to Block BACK
 OTHER SEIZE      INSP2       Capture inspector 2
       ADVANCE    &M(2),&S(2) Inspect shirts
       RELEASE    INSP2       Release inspector 2
 BACK  LEAVE      INSPECT     Free 1 inspection station
       TRANSFER   .05,,REJ    5% are rejected
       TEST E     PH(SLEEVES),1,TYPE2  Is t-shirt short sleeve?
 TYPE1 TERMINATE  1           Short sleeve t-shirts exit the system
 TYPE2 TERMINATE  1           Long sleeve t-shirts exit the system
 REJ   TERMINATE              Bad t-shirts exit the system 
*
*      GPSS/H Control Statements
*
       START      100         Set TG1 to 100 
       END
